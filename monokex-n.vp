// Verifpal 0.19.3
// Monokex, "N" pattern

attacker[active]

principal I[
    knows public pid, zero
    knows private p1
]

principal R[
    knows public pid, zero
    generates R
    RS = G^R
]

R -> I: [RS]

principal I[
    generates E
    IE = G^E

    H0 = HASH(pid)
    H1 = HKDF(RS, H0, nil)
    H2 = HKDF(H1, H1, nil)           // without prelude
    H3 = HKDF(IE, H2, nil)
    H4 = HKDF(RS^E, H3, nil)
    H5, K1 = HKDF(zero, H4, nil)
    S1 = ENC(K1, p1)
    H6, T1 = HKDF(S1, H5, nil)
]

I -> R: IE, S1, T1

principal R[
    _H0 = HASH(pid)
    _H1 = HKDF( RS, _H0, nil)
    _H2 = HKDF(_H1, _H1, nil)           // without prelude
    _H3 = HKDF( IE, _H2, nil)
    _H4 = HKDF(IE^R,_H3, nil)
    _H5, _K1 = HKDF(zero, _H4, nil)
    _H6, _T1 = HKDF(S1, _H5, nil)
    _ = ASSERT(_T1, T1)?
]

queries[
    confidentiality? R
    confidentiality? E
    confidentiality? H6
]
